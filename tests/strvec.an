
type Vec = Str* _data, usz len cap

fun Vec.init := Vec(malloc(8 * Ante.sizeof ""), 0usz, 8usz)

//reserve numElements in Vec v, elements will be uninitialized
fun Vec.reserve: mut Vec v, usz numElems
    if v.len + numElems > v.cap then
        let elemSize = Ante.sizeof ""
        let ptr = realloc (void* v._data) (elemSize*(v.cap + numElems))

        if ptr == void* 0 then
            printf "Error in reserving %u elements for Vec\n" numElems
            return ()

        v._data = ptr
        v.cap += numElems


fun Vec.push: mut Vec v, Str elem
    if v.len >= v.cap then
        v.reserve v.cap

    v#v.len = elem
    v.len += 1

//define the extract operator
fun (#): Vec v, i32 i -> Str
    v._data#i

//define the insert operator
fun (#): mut Vec v, i32 i, Str x
    v._data#i = x


fun print: Vec v
    printne "{ "

    var i = 0
    while i < v.len do
        if i + 1 == v.len then
            printf "\"%s\"" (c8* (v#i))
        else
            printf "\"%s\", " (c8* (v#i))

        i += 1

    print " }"


var v = Vec()

v.push "test1"
v.push "test 2"
v.push "test  3"

print v

free <| v._data
